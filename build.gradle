plugins {
    id 'java'
}

group = 'com.gmail.necnionch.myplugin'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = "opencollab-snapshot"
        url = "https://repo.opencollab.dev/maven-snapshots/"
    }
    maven {
        name = "codemc-snapshots"
        url = "https://repo.codemc.io/repository/maven-snapshots/"
    }
}

configurations {
    collectJars
}

jar {
    from {
        configurations.collectJars.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.13.2-R0.1-SNAPSHOT'
    implementation files("libs/UndineMailer.jar")
    implementation files("libs/LOInfoGUI-1.0.0.jar")
    implementation "org.geysermc.floodgate:api:2.2.0-SNAPSHOT"
    implementation "net.wesjd:anvilgui:1.6.3-SNAPSHOT"
    collectJars "net.wesjd:anvilgui:1.6.3-SNAPSHOT"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
